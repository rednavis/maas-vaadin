plugins {
    id 'java'
    id 'idea'
    id 'org.springframework.boot' version '2.2.4.RELEASE' apply false
    id 'com.google.cloud.tools.jib' version '2.1.0' apply false
    id 'com.vaadin' version '0.5.1' apply false
    id 'com.github.ben-manes.versions' version '0.28.0'
}

ext {
    maasVaadinVersion = '0.0.1-SNAPSHOT'
    lombokVersion = '1.18.12'
    mapstructVersion = '1.3.1.Final'
    springBootVersion = '2.2.4.RELEASE'
    vaadinVersion = '14.1.17'
    jupiterVersion = '5.6.0'
}

allprojects {
    apply plugin: 'project-report'
    // needed because of multi-module configuration
    apply plugin: 'jacoco'
    apply plugin: 'io.spring.dependency-management'

    repositories {
        jcenter()
        mavenCentral()
    }

    dependencyManagement {
        dependencies {
            //Server
            dependency "org.projectlombok:lombok:${lombokVersion}"
            dependency "org.mapstruct:mapstruct:${mapstructVersion}"
            dependency "org.mapstruct:mapstruct-processor:${mapstructVersion}"

            dependency "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"

            imports {
                mavenBom "com.vaadin:vaadin-bom:${vaadinVersion}"
            }

            //Test
            dependency "org.junit.jupiter:junit-jupiter:${jupiterVersion}"
            dependency "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
        }
    }
}

// assumed that all subprojects are java-based modules
subprojects {
    apply from: "${rootDir}/gradle/java.gradle"
    apply from: "${rootDir}/gradle/checkstyle.gradle"

    dependencies {
        implementation 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        implementation 'org.mapstruct:mapstruct'
        annotationProcessor 'org.mapstruct:mapstruct-processor'

        testImplementation('org.junit.jupiter:junit-jupiter') {
            exclude group: 'junit', module: 'junit'
        }
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'junit', module: 'junit'
        }
    }

    jacocoTestReport {
        reports {
            xml.enabled true
            html.enabled false
        }
    }
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        html.enabled false
        csv.enabled false
    }
}

// https://github.com/codecov/example-gradle#-do-you-support-multi-module-projects
codeCoverageReport.dependsOn {
    subprojects*.test
}

//defaultTasks("clean", "vaadinBuildFrontend", "build")

wrapper {
    gradleVersion = "6.2.1"
}